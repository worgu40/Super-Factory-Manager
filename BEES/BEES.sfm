name "BEES" -- Name for the disk

every 1 ticks do
input fe:: from energyCell top side
output fe:: to needsPowers top side
end

every 20 ticks do
    -- Grab from all hives
    input from hive
    output to chest
    forget
    -- Take comb blocks to honeycombs
    input with tag #storage_blocks/honeycombs from chest
    output with tag #storage_blocks/honeycombs to crafter top side
    forget
    -- bring honeycombs back to chest
    input with tag #honeycombs from crafter
    output with tag #honeycombs to chest
    forget
    -- take honeycombs to centrifuge
    input with tag #honeycombs from chest
    output 64 to each centrifuge
    forget
    -- take results from centrifuge
    input from centrifuge
    output EXCEPT *comb* to chest
    forget
    -- take honey to tank
    input fluid:: from centrifuge
    output fluid:: to tank top side
    forget
    -- take gene bottles to centrifuge
    input productivebees:gene_bottle from chest
    output productivebees:gene_bottle to geneCentrifuge
    forget
    -- take gene results to indexer
    input from geneCentrifuge
    output to geneIndexer
    forget
    -- take raw items to ender chest
    input *raw* from chest
    output *raw* to enrichmentChamber top side
    forget
    input from enrichmentChamber bottom side
    output EXCEPT *raw* to chest
    forget
    input with tag #dusts EXCEPT redstone from chest
    output with tag #dusts EXCEPT redstone to enderChest
    forget
    input productivebees:honey_treat from chest
    output productivebees:honey_treat to incubator
    forget
    input productivebees:gene from chest
    output productivebees:gene to geneIndexer
    forget
    input fluid:: from tank bottom side
    output fluid:: to enderTank
    forget
    input productivebees:draconic_dust from chest
    input productivebees:wax from chest
    input the_bumblezone:pollen_puff from chest
    input redstone from chest
    output the_bumblezone:pollen_puff to enderToME
    output productivebees:wax to enderToME
    output productivebees:draconic_dust to enderToME
    output redstone to enderToME
end